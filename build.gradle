plugins {
    id "io.codearte.nexus-staging" version "0.22.0"
    id "io.snyk.gradle.plugin.snykplugin" version "0.4"

    id 'com.github.jk1.dependency-license-report' version '1.16'
}

ext.projectVersion = '2.1.0-SNAPSHOT'
ext.isReleaseVersion = !ext.projectVersion.endsWith('SNAPSHOT')

ext.symphonyRepoUrl = project.properties['symphonyRepoUrl'] ?: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
ext.symphonyRepoUser = project.properties['symphonyRepoUser'] ?: 'Symphony artifactory user'
ext.symphonyRepoPassword = project.properties['symphonyRepoPassword'] ?: 'Symphony artifactory password'

ext.pomDefinition = {
    url = 'https://github.com/SymphonyPlatformSolutions/symphony-api-client-java'
    licenses {
        license {
            name = 'MIT License'
            url = 'http://www.opensource.org/licenses/mit-license.php'
        }
    }
    developers {
        developer {
            name = 'Symphony Platform Solutions'
            email = 'platformsolutions@symphony.com'
            organization = 'Symphony Communication Services'
            organizationUrl = 'https://symphony.com/'
        }
    }
    scm {
        connection = 'scm:git:git://github.com/SymphonyPlatformSolutions/symphony-api-client-java.git'
        developerConnection = 'scm:git:ssh://github.com/SymphonyPlatformSolutions/symphony-api-client-java.git'
        url = 'https://github.com/SymphonyPlatformSolutions/symphony-api-client-java'
    }
}

allprojects {
    group = 'com.symphony.platformsolutions'
    version = projectVersion

    defaultTasks 'build'
}

nexusStaging {
    username = rootProject.ext.symphonyRepoUser
    password = rootProject.ext.symphonyRepoPassword
}

// Required for Snyk plugin to work (https://github.com/snyk/gradle-plugin/issues/1)
repositories {
    mavenCentral()
}

snyk {
    arguments = '--all-sub-projects'
    severity = 'high'
}

licenseReport {
    allowedLicensesFile = new File("$projectDir/.circleci/allowed-licenses.json")
}
