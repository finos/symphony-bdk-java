plugins {
    id "io.github.gradle-nexus.publish-plugin" version "1.3.0"
    id "org.owasp.dependencycheck" version "8.2.1"
}

ext.projectVersion = '2.14.0'
ext.isReleaseVersion = !ext.projectVersion.endsWith('SNAPSHOT')

ext.mavenRepoUrl = project.properties['mavenRepoUrl'] ?: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
ext.mavenRepoUsername = project.properties['mavenRepoUsername'] ?: 'Symphony artifactory user'
ext.mavenRepoPassword = project.properties['mavenRepoPassword'] ?: 'Symphony artifactory password'

ext.pomDefinition = {
    url = 'https://symphony-bdk-java.finos.org'
    licenses {
        license {
            name = 'Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0'
        }
    }
    developers {
        developer {
            name = 'Symphony Platform Solutions'
            email = 'platformsolutions@symphony.com'
            organization = 'Symphony Communication Services'
            organizationUrl = 'https://symphony.com'
        }
    }
    scm {
        connection = 'scm:git:git://github.com/finos/symphony-bdk-java.git'
        developerConnection = 'scm:git:ssh://github.com/finos/symphony-bdk.git'
        url = 'https://github.com/finos/symphony-bdk-java'
    }
}

allprojects {
    group = 'org.finos.symphony.bdk'
    version = projectVersion

    defaultTasks 'build'
}

nexusPublishing {
    packageGroup = 'org.finos'

    repositories {
        sonatype {
            username = project.ext.mavenRepoUsername
            password = project.ext.mavenRepoPassword
        }
    }
}

repositories {
    mavenCentral()
}

dependencyCheck {
    failBuildOnCVSS=5
    suppressionFile="./allow-list.xml"
}
