plugins {
    id 'java-platform'
    id 'maven-publish'
    id 'signing'
}

description = 'Symphony Java BDK Bom (Bill Of Materials)'

javaPlatform {
    allowDependencies()
}

repositories {
    mavenCentral()
}

dependencies {
    // import Spring Boot's BOM
    api platform('org.springframework.boot:spring-boot-dependencies:2.6.8')
    // import Jackson's BOM
    api platform('com.fasterxml.jackson:jackson-bom:2.13.2.20220328')
    // define all our dependencies versions
    constraints {
        // Internal modules dependencies (Keep them first)
        api "org.finos.symphony.bdk:symphony-bdk-core:$project.version"
        api "org.finos.symphony.bdk:symphony-bdk-config:$project.version"
        api "org.finos.symphony.bdk:symphony-bdk-extension-api:$project.version"
        api "org.finos.symphony.bdk:symphony-bdk-http-api:$project.version"
        api "org.finos.symphony.bdk:symphony-bdk-http-jersey2:$project.version"
        api "org.finos.symphony.bdk:symphony-bdk-http-webclient:$project.version"
        api "org.finos.symphony.bdk:symphony-bdk-core-spring-boot-starter:$project.version"
        api "org.finos.symphony.bdk:symphony-bdk-app-spring-boot-starter:$project.version"
        api "org.finos.symphony.bdk:symphony-bdk-template-api:$project.version"
        api "org.finos.symphony.bdk:symphony-bdk-template-freemarker:$project.version"
        api "org.finos.symphony.bdk:symphony-bdk-template-handlebars:$project.version"
        // extensions
        api "org.finos.symphony.bdk.ext:symphony-group-extension:$project.version"

        // External dependencies
        api 'org.projectlombok:lombok:1.18.22'

        api 'org.apiguardian:apiguardian-api:1.1.2'

        api 'org.slf4j:slf4j-api:1.7.36'
        api 'org.slf4j:slf4j-log4j12:1.7.36'

        // Logback is used by default for Spring based projects, force the version for LOGBACK-1591
        api 'ch.qos.logback:logback-classic:1.2.8'
        api 'ch.qos.logback:logback-core:1.2.8'

        // Just in case users are using log4j instead of logback (default), for CVE-2021-44228
        api 'org.apache.logging.log4j:log4j-api:2.17.2'
        api 'org.apache.logging.log4j:log4j-core:2.17.2'
        api 'org.apache.logging.log4j:log4j-slf4j-impl:2.17.2'
        api 'org.apache.logging.log4j:log4j-jul:2.17.2'

        api 'commons-io:commons-io:2.11.0'
        api 'commons-codec:commons-codec:1.15'
        api 'commons-beanutils:commons-beanutils:1.9.4'
        api 'org.apache.commons:commons-lang3:3.12.0'
        api 'org.apache.commons:commons-text:1.9'
        api 'commons-logging:commons-logging:1.2'
        api 'com.brsanthu:migbase64:2.2'
        api 'io.jsonwebtoken:jjwt:0.9.1'
        api 'org.bouncycastle:bcpkix-jdk15on:1.70'
        api 'com.google.code.findbugs:jsr305:3.0.2'

        api 'io.github.resilience4j:resilience4j-retry:1.7.1'

        api 'io.swagger:swagger-annotations:1.6.0'
        api 'org.openapitools:jackson-databind-nullable:0.2.2'

        api 'org.glassfish.jersey.core:jersey-client:2.35'
        api 'org.glassfish.jersey.core:jersey-common:2.35'
        api 'org.glassfish.jersey.inject:jersey-hk2:2.35'
        api 'org.glassfish.jersey.media:jersey-media-multipart:2.35'
        api 'org.glassfish.jersey.media:jersey-media-json-jackson:2.35'
        api 'org.glassfish.jersey.connectors:jersey-apache-connector:2.35'
        api 'org.glassfish.jersey.ext:jersey-entity-filtering:2.35'

        api 'org.projectreactor:reactor-spring:1.0.1.RELEASE'

        api 'org.freemarker:freemarker:2.3.31'
        api 'com.github.jknack:handlebars:4.3.0'
        api 'org.reflections:reflections:0.9.12'

        api 'org.junit.jupiter:junit-jupiter:5.8.2'
        api 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
        api 'com.tngtech.archunit:archunit-junit5:0.22.0'
        api 'org.mock-server:mockserver-netty:5.12.0'
        api 'org.mockito:mockito-core:4.3.1'
        api 'org.mockito:mockito-junit-jupiter:4.3.1'
        api 'org.assertj:assertj-core:3.22.0'
    }
}

publishing {
    publications {
        bom(MavenPublication) {
            from components.javaPlatform
            pom(rootProject.ext.pomDefinition)
            pom {
                name = project.name
                description = project.description
            }
        }
    }

    repositories {
        maven {
            credentials {
                username rootProject.ext.mavenRepoUsername
                password rootProject.ext.mavenRepoPassword
            }
            url rootProject.ext.mavenRepoUrl
        }
    }
}

signing {
    required { rootProject.isReleaseVersion }
    sign publishing.publications.bom
}
